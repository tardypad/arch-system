pkgname=neovim-formatter-git
pkgdesc="A format runner for Neovim"
pkgver=r202.b9d7f85
pkgrel=1
arch=("any")
url="https://github.com/mhartington/formatter.nvim"
license=("MIT")

groups=(
  'neovim-plugins'
)

makedepends=(
  'git'
)

source=(
  "git+https://github.com/mhartington/formatter.nvim.git"
)

sha512sums=(
  'SKIP'
)

pkgver() {
  cd formatter.nvim
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  depends+=(
    'neovim'
  )

  cd formatter.nvim
  find lua plugin doc -type f -exec install -D -m644 '{}' "$pkgdir/usr/share/nvim/site/pack/dist/start/${pkgname}/{}" \;
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
