pkgbase=dotfiles-git
pkgname=(
  'dotfiles-light-git'
  'dotfiles-dark-git'
)
pkgver=r3004.cebf66a1
pkgrel=4
pkgdesc="My dotfiles"
arch=('any')
url="https://github.com/tardypad/dotfiles"
license=('custom')

groups=(
  'tardypad'
)

conflicts=(
  'dotfiles-git'
)

provides=(
  'dotfiles-git'
)

replaces=(
  'dotfiles-git'
)

makedepends=(
  'curl'
  'git'
  'openssh'
  'scdoc'
)

depends=(
  'fontconfig'
  'systemd'
  'vim'
)

install=dotfiles-git.install

# to be changed depending on the machine
_branch=master

# use personal remote containing all the branches
source=(
  "git+ssh://damien@tardypad.me:11235/~/repos/dotfiles#branch=${_branch}"
)

sha512sums=(
  'SKIP'
)

pkgver() {
  cd dotfiles
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_dotfiles-light-git() {
  cd dotfiles
  make DESTDIR="$pkgdir" THEME=light all

  adjust_permissions
  hush_login
}

package_dotfiles-dark-git() {
  cd dotfiles
  make DESTDIR="$pkgdir" THEME=dark all

  adjust_permissions
  hush_login
}

adjust_permissions() {
  chown -R damien:damien "$pkgdir/home/damien"
  chmod 711 "$pkgdir/home/damien"
  chmod 700 "$pkgdir/home/damien/.config"
}

hush_login() {
  printf '' > "$pkgdir/home/damien/.hushlogin"
  chown damien:damien "$pkgdir/home/damien/.hushlogin"
}
